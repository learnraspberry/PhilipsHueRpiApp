buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.2.RELEASE")
    }
}

plugins {
    id "com.moowork.node" version "1.3.1"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar {
    baseName = 'philipeHueRpiApp'
    version =  '1.0'
}

repositories {
    mavenCentral()
}

node {
    download = true
    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")
}

task appNpmInstall(type: NpmTask) {
    description = "Installs all dependencies from package.json"
    workingDir = file("${project.projectDir}/frontend/")
    args = ["install"]
}

task appNpmBuild(type: NpmTask) {
    description = "Builds production version of the webapp"
    workingDir = file("${project.projectDir}/frontend")
    args = ["run", "build"]
    mustRunAfter appNpmInstall
}

task copyWebApp(type: Copy) {
    from 'frontend/build'
    into 'build/resources/main/static/.'
    mustRunAfter appNpmBuild

}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
    compile("org.springframework.boot:spring-boot-starter-web")
    testCompile("junit:junit")
}

bootJar.dependsOn appNpmInstall
bootJar.dependsOn appNpmBuild
bootJar.dependsOn copyWebApp
